#!/usr/bin/env runawk

$1 ~ /PKGPATH=/ {
	pkgpath = substr($0, 9)
	next
}

$1 ~ /_VARIANTS=/ {
	variants = substr($0, 11)
	next
}

NF == 0 {
	$0 = variants
	count = 0
	for (k=1; k <= NF; ++k){
		values = varname = $k
		sub(/=.*$/, "", varname)
		sub(/^[^=]*=/, "", values)

		cnt = split(values, vals, /,/)

		if (count){
			new_count = count
			for (i=1; i <= count; ++i){
				if (!(i in variant)) continue

				for (j=1; j <= cnt; ++j){
					++new_count
					variant [new_count] = (variant [i] "," varname "=" vals [j])
				}
				delete variant [i]
			}
			count = new_count
		}else{
			for (j=1; j <= cnt; ++j){
				variant [j] = (varname "=" vals [j])
			}
			count = cnt
		}
	}

	for (i = 1; i <= count; ++i){
		if (i in variant)
			print pkgpath ":" variant [i]
	}
	pkgpath = variants = ""

	delete variant
}
