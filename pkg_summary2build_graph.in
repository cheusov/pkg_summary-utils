#!/usr/bin/env runawk

# Copyright (c) 2008-2009 Aleksey Cheusov <vle@gmx.net>
#
# Permission is hereby granted, free of charge, to any person obtaining
# a copy of this software and associated documentation files (the
# "Software"), to deal in the Software without restriction, including
# without limitation the rights to use, copy, modify, merge, publish,
# distribute, sublicense, and/or sell copies of the Software, and to
# permit persons to whom the Software is furnished to do so, subject to
# the following conditions:
#
# The above copyright notice and this permission notice shall be
# included in all copies or substantial portions of the Software.
#
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
# EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
# MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND
# NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE
# LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION
# OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION
# WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

############################################################

#env "LC_ALL=C"

#use "power_getopt.awk"
#use "alt_assert.awk"
#use "braceexpand.awk"

############################################################
#.begin-str help
# pkg_summary2build_graph - converts src_summary to build
#   dependency graph analysing {alter,native}:../../dep/endencies entries
#   in DEPENDS and BUILD_DEPENDS fields.
# Format of the output:
#   package1
#   package2 package3
#   ...
# Here package3 depends on package2, package1 has no dependencies.
# usage: pkg_summary2build_graph [OPTIONS] [files...]
# OPTIONS:
#   -h|--help            display this help
#.end-str
############################################################

/^PKGNAME=/ {
	pkgname = substr($0, 9)
	pkgbase = pkgname
	sub(/-[^-]+$/, "", pkgbase)

	pkgpaths [pkgbase] = 1

	next
}

/^PKGPATH=/ {
	pkgpath = substr($0, 9)
	nodep_pkgpaths [pkgpath] = 1
	next
}

NF == 0 {
	assert(pkgbase != "", "Cannot find pkgbase")
	assert(pkgpath != "", "Cannot find pkgpath")

	pkgpaths [pkgbase] = pkgpath

	pkgbase = pkgpath = ""
	next
}

/^DEPENDS=/ || /^BUILD_DEPENDS=/ {
	sub(/^[^=]+=/, "")
	delete deps
	for (i=1; i <= NF; ++i){
		if ($i !~ /[{]/){
			sub(/^.*:(..\/..\/)?/, "", $i)
			deps [$i " " pkgpath] = 1
			delete nodep_pkgpaths [pkgpath]
			delete nodep_pkgpaths [$i]
		}else{
			depbase = $i
			sub(/:.*$/, "", depbase)
			depbase = braceexpand(depbase)
			sub(/ .*$/, "", depbase)
			sub(/-\[[^\[\]]*\].$|(>|>=|<|<=|=|-)[^><=-]*$/, "", depbase)

			delete nodep_pkgpaths [pkgpath]
			hidden_deps [depbase " " pkgpath] = 1
		}
	}
	for (dep in deps){
		print dep
	}
}

END {
	ex = 0

	for (hdep in hidden_deps){
		base = hdep
		sub(/ .*$/, "", base)

		if (! (base in pkgpaths)){
			print "Cannot find PKGPATH for " base > "/dev/stderr"
			ex = 1
		}else{
			pkgpath = hdep
			sub(/^.* /, "", pkgpath)

			print pkgpaths [base], pkgpath
		}
	}

	for (pkgpath in nodep_pkgpaths){
		print pkgpath
	}

	exit ex
}
