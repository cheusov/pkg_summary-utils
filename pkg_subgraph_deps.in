#!/usr/bin/env runawk

# Copyright (c) 2010, Aleksey Cheusov <vle@gmx.net>
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are
# met:
#
#     * Redistributions of source code must retain the above copyright
#       notice, this list of conditions and the following disclaimer.
#     * Redistributions in binary form must reproduce the above
#       copyright notice, this list of conditions and the following
#       disclaimer in the documentation and/or other materials provided
#       with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
# "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
# LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
# A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
# HOLDER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
# SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
# LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
# DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
# THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
# OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

#use "power_getopt.awk"
#use "xgetline.awk"
#use "xclose.awk"

#.begin-str help
# This program takes a dependency graph of packages and a list of
# individual packages and outputs subgraph consisting of packages that
# depend on listed packages directly or indirectly (the default).
#
# usage: pkg_subgraph_deps [OPTIONS] [files...]
# OPTIONS:
#    -h           display this screen
#    =f <file>    list of packages, one package per line
#    =p <pkgs>    list of packages separated by space character
#    -r           output dependencies
#    -x           exclude packages in the list
#    -v           invert condition
#    -n           output nodes, not edges
#    -t           produce output compatible with tsort(1) command
# Input graph is represented by list of edges and isolated nodes, e.g.
#   devel/gmake textproc/dict-client
#   devel/gmake textproc/dict-server
#   devel/libjudy
#   devel/libmaa textproc/dict-client
#   devel/libmaa textproc/dict-server
#   devel/libmaa wip/paexec
#   net/netcat
#
# Option -f or -p are mandatory.
#.end-str

BEGIN {
	if (getarg("h")){
		print_help()
		exitnow(0)
	}

	fn         = getarg("f")
	pkgs       = getarg("p")
	opt_deps   = getarg("r")
	opt_excl   = getarg("x")
	opt_invert = getarg("v")
	opt_nodes  = getarg("n")
	opt_tsort  = getarg("t")

	if (!fn && !pkgs){
		print "-f or -p are mandatory!" > "/dev/stderr"
		exitnow(1)
	}

	if (pkgs != ""){
		split(pkgs, pkgs_arr)
		for (i in pkgs_arr)
			list [pkgs_arr [i]] = 1
	}else if (fn != ""){
		while (xgetline0(fn))
			list [$1] = 1

		xclose(fn)
	}
}

NF == 1 || (NF == 2 && $1 == $2) {
	nodes [$1] = 1
	next
}

NF == 2 {
	if (opt_deps)
		edge [$2, ++count [$2]] = $1
	else
		edge [$1, ++count [$1]] = $2

	nodes [$1] = nodes [$2] = 1
	next
}

function rec (pkg,                    i,to){
	if (pkg in result)
		return

	if (! (pkg in count))
		return

	result [pkg] = 1
	for (i=1; i <= count [pkg]; ++i){
		to = edge [pkg, i]
		rec(to)
		result [to] = 1
	}
}

function print_edge (from, to){
	if ((from in result) && (to in result)){
		print from, to
		printed [from] = printed [to] = 1
	}
}

END {
	for (i in list){
		rec(i)
		result [i] = 1
	}
	if (opt_excl){
		for (i in list)
			delete result [i]
	}
	if (opt_invert){
		for (i in nodes){
			if (i in result)
				delete result [i]
			else
				result [i] = 1
		}
	}

	if (!opt_nodes){
		for (i in edge){
			from = substr(i, 1, index(i, SUBSEP)-1)

			if (opt_deps)
				print_edge(edge [i], from)
			else
				print_edge(from, edge [i])
		}
	}

	for (i in nodes){
		if ((i in result) && !(i in printed)){
			if (opt_tsort)
				print i, i
			else
				print i
		}
	}
}
